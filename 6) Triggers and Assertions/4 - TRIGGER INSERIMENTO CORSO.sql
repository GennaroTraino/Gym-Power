-- TRIGGER PER CONTROLLARE L'INSERIMENTO DI UN CORSO
CREATE OR REPLACE TRIGGER INSERIMENTO_CORSO
BEFORE INSERT ON CORSO
FOR EACH ROW

DECLARE 
	N_CORSI_ISTRUTTORE  NUMBER;
	N_CORSI_STRUTTURA  NUMBER;
	DATA_INFERIORE  EXCEPTION;
	TROPPO_LAVORO  EXCEPTION;
	TROPPI_CORSI  EXCEPTION;


BEGIN
-- NON SI PUO' INSERIRE UN CORSO CHE ABBIA DATA INIZIALE MINORE DELLA DATA CORRENTE
	IF(:NEW.DATA_INIZIO_CORSO < SYSDATE)
		THEN RAISE DATA_INFERIORE;
	END IF;

-- VERIFICA QUANTI CORSI STA TENENDO L'ISTRUTTORE IN CONTEMPORANEA
-- SE LAVORA A PIU DI 4 CORSI ALLORA LANCIA UN ECCEZIONE
	SELECT COUNT(*) INTO N_CORSI_ISTRUTTORE
	FROM ISTRUTTORE JOIN CORSO  ON CF_ISTRUTTORE = CF_ISTRUTTORE_CORSO
	WHERE CF_ISTRUTTORE_CORSO = :NEW.CF_ISTRUTTORE_CORSO 
		AND DATA_FINE_CORSO > SYSDATE;

	IF(N_CORSI_ISTRUTTORE > 3) 
		THEN RAISE TROPPO_LAVORO;
	END IF;


-- VERIFICA QUANTI CORSI TIENE UNA STRUTTURA CONTEMPORANEAMENTE,
-- SE CI SONO PIU' DI 8 CORSI LANCIA UN ECCEZIONE
	SELECT COUNT(*) INTO N_CORSI_STRUTTURA
	FROM CORSO JOIN STRUTTURA ON CAP_CORSO = CAP
	WHERE CAP_CORSO = :NEW.CAP_CORSO
		AND DATA_FINE_CORSO > SYSDATE;

	IF(N_CORSI_STRUTTURA > 8) 
		THEN RAISE TROPPI_CORSI;
	END IF;


EXCEPTION 
	WHEN DATA_INFERIORE THEN 
		RAISE_APPLICATION_ERROR(-20505,'E'' STATA INSERITA UNA DATA INIZIO CORSO PRECEDENTE A QUELLA DI OGGI');
	WHEN TROPPO_LAVORO THEN 
		RAISE_APPLICATION_ERROR(-20506,'ISTRUTTORE STA SEGUENDO GIA 4 CORSI');
	WHEN TROPPI_CORSI THEN 
		RAISE_APPLICATION_ERROR(-20507,'TROPPI CORSI PER QUESTA STRUTTURA');

END;
/
